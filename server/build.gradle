plugins {
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
}

def setupAndroidPlugin = { Project project ->
    project.repositories {
        google()
        mavenCentral()
    }

    project.android {
        compileSdk 36
        buildToolsVersion '36.0.0'

        defaultConfig {
            minSdk 29
            targetSdk 36
        }

        compileOptions {
            coreLibraryDesugaringEnabled true
        }
    }

    project.dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    }
}

def setupKotlinAndroidPlugin = { project ->
    project.kotlin {
        jvmToolchain 17
    }

    project.android {
        kotlinOptions {
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        }
    }
}

subprojects {
    project.plugins.whenPluginAdded {
        switch (it.class.name) {
            case 'com.android.build.gradle.AppPlugin':
                // fallthrough.
            case 'com.android.build.gradle.LibraryPlugin':
                setupAndroidPlugin(project)
                break
            case 'org.jetbrains.kotlin.gradle.plugin.KotlinAndroidPluginWrapper':
                setupKotlinAndroidPlugin(project)
                break
            default:
                break
        }
    }
}

tasks.register('clean', Delete) {
    group 'build'

    delete rootProject.layout.buildDirectory
}
